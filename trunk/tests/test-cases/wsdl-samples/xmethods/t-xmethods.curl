||| Copyright (C) 1998-2009, Sumisho Computer Systems Corp.  All Rights Reserved.
|||
||| An OpenCurl project.

||| Licensed under the Apache License, Version 2.0 (the "License");
||| you may not use this file except in compliance with the License.
||| You may obtain a copy of the License at
||| 
|||     http://www.apache.org/licenses/LICENSE-2.0
||| 
||| Unless required by applicable law or agreed to in writing, software
||| distributed under the License is distributed on an "AS IS" BASIS,
||| WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
||| See the License for the specific language governing permissions and
||| limitations under the License.

{curl 6.0 applet}
{applet manifest = "manifest.mcurl"}

{import * from COM.CURL.WSDK.XMETHODS-SUPPORT}

{value
    let rs:RecordSet = 
        {load-services-recordset {xmethods-recordset}}
    let rg:RecordGrid =
        {RecordGrid
            record-source = rs,
            sort = "filename",
            width = {add-stretch},
            automatic-columns? = false,
            {RecordGridColumn "filename",
                width = 2in},
            {RecordGridColumn "name",
                width = 2in},
            {RecordGridColumn "status",
                width = 1in},
            {RecordGridColumn "note",
                width = 1.5in},
            {RecordGridColumn "wsdl-url",
                width = 1.5in}}
    let rf:RecordForm =
        {RecordForm
            record-source = rg.records,
            display-navigation-panel? = false,
            {VBox
                margin = 3pt,
                spacing = 3pt,
                hstretch? = true,
                {HBox
                    spacing = 3pt,
                    {DisplayFrame 
                        font-weight = "bold",
                        {bind value to "name"}},
                    {DisplayFrame {bind value to "filename"}},
                    {DropdownList
                        width = 1in,
                        vorigin = 70%,
                        {splice service-status-codes},
                        {bind value to "status"},
                        {on ValueFinished at c:DropdownList do
                            {c.update-data-binding "value"}}},
                    {Fill}},
                {italic Description},
                {TextArea enabled? = false,
                    {bind value to "description"}},
                {italic Note},
                {TextArea {bind value to "note"}},
                {HBox
                    spacing = 3pt,
                    {text WSDL},
                    {DisplayFrame
                        {bind value to "wsdl-url",
                            {format loc:String as 
                                {link target = "WSDL",
                                    href = {url loc}, 
                                    {value loc}}}}},
                    {text SOAPScope},
                    {DisplayFrame
                        {bind value to "wsdl-url",
                            {format loc:String as 
                                {soapscope-link loc}}}}
                }}}
    || focus detail display on current record
    {rg.add-event-handler
        {on CurrentRecordChanged do
            set rf.current-index = rg.current-index}}
    
    let base-url:Url = {url "."}
    let directory:Url = {url "services"}
    let directory-display:TextDisplay = 
        {TextDisplay 
            tooltip = {Tooltip directory},
            value = {directory.name-relative-to-url base-url},
            width = 1.5in}
    {VBox
        font-size = 9pt,
        {HBox
            spacing = 3pt,
            margin = 3pt,
            {title font-size = 15pt, WSDK Test Generator}, 
            {paragraph Source 
                {link href = {url "http://www.xmethods.com"}, 
                target = "WSDL", font-size = 12pt,
                www.xmethods.com}},
            {Fill},       
            {CommandButton 
                label = {message Copy ...},
                {on Action do                     
                    {if {verify-directory directory} then
                        {with-busy-cursor
                            {copy-services rg, directory}
                        }}}},
            {text Directory},
            directory-display,            
            {CommandButton label = {message Choose ...},
                {on Action do 
                    {if-non-null loc =
                        {choose-directory 
                            default-location = directory}
                     then  
                        {if {verify-directory loc} then
                            set directory-display.tooltip =
                                {Tooltip loc}
                            set directory-display.value =
                                {loc.name-relative-to-url base-url}
                            set directory = loc}
                    }}},
            {link href = {url "xmethods-info.curl"}, target = "_INFO", {big {bold Help}}}},
        {hrule},
        {text-width-display rg},
        {hrule},
        rf}
}
                
